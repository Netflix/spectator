buildscript {
  repositories { jcenter() }
  dependencies {
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.12.0'
    classpath 'com.netflix.nebula:nebula-dependency-recommender:3.6.3'

    // This is a workaround for getting the shadow plugin to work correctly
    // for some sub-projects. For more details see:
    // https://github.com/johnrengelman/shadow/issues/188
    classpath 'org.apache.ant:ant:1.9.7'
  }
}

plugins {
  id 'nebula.netflixoss' version '3.4.0'
  id 'me.champeau.gradle.jmh' version '0.3.0'
}

// Establish version and status
ext.githubProjectName = 'spectator'

allprojects {
  apply plugin: 'nebula.dependency-recommender'
  apply plugin: 'project-report'
  apply plugin: 'me.champeau.gradle.jmh'
  apply plugin: 'com.github.ben-manes.versions'

  task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
  }
}

subprojects {
  apply plugin: 'nebula.netflixoss'
  apply plugin: 'nebula.compile-api'
  apply plugin: 'java'
  apply plugin: 'build-dashboard'
  apply plugin: 'jacoco'
  apply plugin: 'checkstyle'
  apply plugin: 'findbugs'
  apply plugin: 'pmd'

  repositories {
    jcenter()
    mavenLocal()
  }

  group = "com.netflix.${githubProjectName}"

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  project.tasks.withType(Javadoc) {
    if (JavaVersion.current().isJava8Compatible()) {
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }
  javadoc {
    options {
      links = ['http://docs.oracle.com/javase/8/docs/api/']
    }
  }

  dependencyRecommendations {
    propertiesFile file: new File(rootProject.projectDir, 'dependencies.properties')
  }

  dependencies {
    compile "org.slf4j:slf4j-api"
    testCompile 'junit:junit'
    testCompile 'nl.jqno.equalsverifier:equalsverifier'
    jmh "org.slf4j:slf4j-simple"
  }

  jmh {
    warmupIterations = 2
    iterations = 10
    fork = 5
    profilers = ['stack']
    include '.*Ids.*'
  }

  jacoco {
    toolVersion = "0.7.6.201602180812"
  }

  jacocoTestReport {
    additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
    reports {
      xml.enabled false
      csv.enabled false
      html.destination "${buildDir}/reports/jacoco"
    }
  }
  
  checkstyle {
    toolVersion = '6.7'
    ignoreFailures = false 
    configFile = rootProject.file('codequality/checkstyle.xml')
    sourceSets = [sourceSets.main]
  }
  tasks.withType(Checkstyle) {
    exclude '**/tdunning/**'
  }
  
  findbugs {
    excludeFilter = rootProject.file('codequality/findbugs-exclude.xml')
    ignoreFailures = false
    sourceSets = [sourceSets.main]
  }
  tasks.withType(FindBugs) {
    findbugs.toolVersion "3.0.0"
    reports {
      xml.enabled = true
      html.enabled = false
    }
  }

  pmd {
    toolVersion = '5.3.3'
    ignoreFailures = false
    sourceSets = [sourceSets.main]
    ruleSets = []
    ruleSetFiles = rootProject.files("codequality/pmd.xml")
  }
  tasks.withType(Pmd) {
    exclude '**/tdunning/**'
  }

  task copyDepsToBuild << {
    ['compile', 'runtime', 'testCompile', 'testRuntime'].each { conf ->
      delete "${buildDir}/dependencies/${conf}"
      copy {
        from configurations[conf]
        into "${buildDir}/dependencies/${conf}"
      }
    }
  }
}

