buildscript {
  repositories { jcenter() }
  dependencies {
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    classpath 'com.netflix.nebula:nebula-dependency-recommender:6.1.1'
    classpath 'com.netflix.nebula:gradle-netflixoss-project-plugin:7.0.0'

    // This is a workaround for getting the shadow plugin to work correctly
    // for some sub-projects. For more details see:
    // https://github.com/johnrengelman/shadow/issues/188
    classpath 'org.apache.ant:ant:1.9.7'
  }
}

plugins {
  id 'me.champeau.gradle.jmh' version '0.4.7'
  id "com.github.spotbugs" version "1.6.9" apply false
}

// Establish version and status
ext.githubProjectName = 'spectator'

allprojects {
  apply plugin: 'nebula.dependency-recommender'
  apply plugin: 'project-report'
  apply plugin: 'me.champeau.gradle.jmh'
  apply plugin: 'com.github.ben-manes.versions'
  apply plugin: 'nebula.netflixoss'
}

subprojects {
  apply plugin: 'nebula.compile-api'
  apply plugin: 'java'
  apply plugin: 'build-dashboard'
  apply plugin: 'com.github.spotbugs'
  apply plugin: 'checkstyle'
  apply plugin: 'pmd'

  repositories {
    jcenter()
    mavenLocal()
  }

  group = "com.netflix.${githubProjectName}"

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  project.tasks.withType(Javadoc) {
    if (JavaVersion.current().isJava8Compatible()) {
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }
  javadoc {
    options {
      links = ['http://docs.oracle.com/javase/8/docs/api/']
    }
  }

  dependencyRecommendations {
    propertiesFile file: new File(rootProject.projectDir, 'dependencies.properties')
  }

  dependencies {
    compile "org.slf4j:slf4j-api"
    testCompile 'org.junit.jupiter:junit-jupiter-engine'
    testCompile 'nl.jqno.equalsverifier:equalsverifier'
    jmh "org.slf4j:slf4j-simple"
    jmh "org.openjdk.jmh:jmh-core:1.19"
    jmh "org.openjdk.jmh:jmh-generator-annprocess:1.19"
  }

  test {
    useJUnitPlatform()
  }

  jmh {
    jmhVersion = '1.21'
    warmupIterations = 5
    iterations = 10
    fork = 1
    threads = 1
    profilers = ['stack', 'gc']
    includeTests = false
    duplicateClassesStrategy = 'warn'
    include = ['.*PatternMatching.*']
  }

  checkstyle {
    toolVersion = '8.14'
    ignoreFailures = false 
    configFile = rootProject.file('codequality/checkstyle.xml')
    sourceSets = [sourceSets.main]
  }
  
  spotbugs {
    toolVersion = '3.1.10'
    excludeFilter = rootProject.file('codequality/findbugs-exclude.xml')
    ignoreFailures = false
    sourceSets = [sourceSets.main]
  }
  tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
      xml.enabled = false
      html.enabled = true
    }
  }

  pmd {
    toolVersion = '6.9.0'
    ignoreFailures = false
    sourceSets = [sourceSets.main]
    ruleSets = []
    ruleSetFiles = rootProject.files("codequality/pmd.xml")
  }
}

