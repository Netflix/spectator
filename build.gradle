buildscript {
  repositories { jcenter() }
  dependencies {
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.36.0'
    classpath 'com.netflix.nebula:nebula-dependency-recommender:9.1.1'
    classpath 'com.netflix.nebula:gradle-netflixoss-project-plugin:8.10.0'
  }
}

plugins {
  id 'me.champeau.gradle.jmh' version '0.5.2'
  id "com.github.spotbugs" version "4.6.0" apply false
}

// Establish version and status
ext.githubProjectName = 'spectator'

allprojects {
  apply plugin: 'nebula.dependency-recommender'
  apply plugin: 'project-report'
  apply plugin: 'me.champeau.gradle.jmh'
  apply plugin: 'com.github.ben-manes.versions'
  apply plugin: 'nebula.netflixoss'
}

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'build-dashboard'
  apply plugin: 'com.github.spotbugs'
  apply plugin: 'checkstyle'
  apply plugin: 'pmd'

  repositories {
    jcenter()
    mavenLocal()
  }

  group = "com.netflix.${githubProjectName}"

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  tasks {
    compileJava {
      options.encoding = 'UTF-8'
      // Generate java8 bytecode and compile against the java8 class
      // libraries.
      if (JavaVersion.current().isJava9Compatible()) {
        options.release = 8
      }
    }
    compileTestJava {
      options.encoding = 'UTF-8'
      if (JavaVersion.current().isJava9Compatible()) {
        options.release = JavaVersion.current().majorVersion.toInteger()
      }
    }

    javadoc {
      if (JavaVersion.current().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
      }
      if (JavaVersion.current().isJava9Compatible()) {
        options.addStringOption('-release', '8')
      }
    }
  }

  javadoc {
    options {
      links = ['https://docs.oracle.com/javase/8/docs/api/']
    }
  }

  dependencyRecommendations {
    propertiesFile file: new File(rootProject.projectDir, 'dependencies.properties')
  }

  dependencies {
    implementation "org.slf4j:slf4j-api"
    testImplementation 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'nl.jqno.equalsverifier:equalsverifier'
    jmh "org.slf4j:slf4j-simple"
    jmh "org.openjdk.jmh:jmh-core:1.27"
    jmh "org.openjdk.jmh:jmh-generator-annprocess:1.27"
  }

  test {
    useJUnitPlatform()
  }

  jmh {
    jmhVersion = '1.27'
    warmupIterations = 2
    iterations = 5
    fork = 1
    //threads = 1
    profilers = ['stack', 'gc']
    includeTests = false
    duplicateClassesStrategy = 'warn'
    include = ['.*Ids.*']
  }

  checkstyle {
    toolVersion = '8.39'
    ignoreFailures = false 
    configFile = rootProject.file('codequality/checkstyle.xml')
    sourceSets = [sourceSets.main]
  }
  
  spotbugs {
    toolVersion = '4.2.0'
    excludeFilter = rootProject.file('codequality/findbugs-exclude.xml')
    ignoreFailures = false
    spotbugsMain.enabled = true
    spotbugsTest.enabled = false
    spotbugsJmh.enabled = false
  }
  spotbugsMain {
    reports {
      xml.enabled = false
      html.enabled = true
    }
  }

  pmd {
    toolVersion = '6.30.0'
    ignoreFailures = false
    sourceSets = [sourceSets.main]
    ruleSets = []
    ruleSetFiles = rootProject.files("codequality/pmd.xml")
  }
}

