buildscript {
  repositories { jcenter() }
  dependencies {
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.33.0'
    classpath 'com.netflix.nebula:nebula-dependency-recommender:9.0.2'
    classpath 'com.netflix.nebula:gradle-netflixoss-project-plugin:8.8.1'
  }
}

plugins {
  id 'me.champeau.gradle.jmh' version '0.5.0'
  id "com.github.spotbugs" version "4.5.1" apply false
}

// Establish version and status
ext.githubProjectName = 'spectator'

allprojects {
  apply plugin: 'nebula.dependency-recommender'
  apply plugin: 'project-report'
  apply plugin: 'me.champeau.gradle.jmh'
  apply plugin: 'com.github.ben-manes.versions'
  apply plugin: 'nebula.netflixoss'
}

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'build-dashboard'
  apply plugin: 'com.github.spotbugs'
  apply plugin: 'checkstyle'
  apply plugin: 'pmd'

  repositories {
    jcenter()
    mavenLocal()
  }

  group = "com.netflix.${githubProjectName}"

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  tasks {
    compileJava {
      options.encoding = 'UTF-8'
      // Generate java8 bytecode and compile against the java8 class
      // libraries.
      if (JavaVersion.current().isJava9Compatible()) {
        options.compilerArgs.addAll(['--release', '8'])
      }
    }
    compileTestJava {
      options.encoding = 'UTF-8'
      sourceCompatibility = JavaVersion.current()
      targetCompatibility = JavaVersion.current()
    }

    javadoc {
      if (JavaVersion.current().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
      }
      if (JavaVersion.current().isJava9Compatible()) {
        options.addStringOption('-release', '8')
      }
    }
  }

  javadoc {
    options {
      links = ['https://docs.oracle.com/javase/8/docs/api/']
    }
  }

  dependencyRecommendations {
    propertiesFile file: new File(rootProject.projectDir, 'dependencies.properties')
  }

  dependencies {
    implementation "org.slf4j:slf4j-api"
    testImplementation 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'nl.jqno.equalsverifier:equalsverifier'
    jmh "org.slf4j:slf4j-simple"
    jmh "org.openjdk.jmh:jmh-core:1.25"
    jmh "org.openjdk.jmh:jmh-generator-annprocess:1.25"
  }

  test {
    useJUnitPlatform()
  }

  jmh {
    jmhVersion = '1.25'
    warmupIterations = 2
    iterations = 5
    fork = 1
    //threads = 1
    profilers = ['stack', 'gc']
    includeTests = false
    duplicateClassesStrategy = 'warn'
    include = ['.*PollMetersBench.*']
  }

  checkstyle {
    toolVersion = '8.32'
    ignoreFailures = false 
    configFile = rootProject.file('codequality/checkstyle.xml')
    sourceSets = [sourceSets.main]
  }
  
  spotbugs {
    toolVersion = '4.1.3'
    excludeFilter = rootProject.file('codequality/findbugs-exclude.xml')
    ignoreFailures = false
    spotbugsMain.enabled = true
    spotbugsTest.enabled = false
    spotbugsJmh.enabled = false
  }
  spotbugsMain {
    reports {
      xml.enabled = false
      html.enabled = true
    }
  }

  pmd {
    toolVersion = '6.23.0'
    ignoreFailures = false
    sourceSets = [sourceSets.main]
    ruleSets = []
    ruleSetFiles = rootProject.files("codequality/pmd.xml")
  }
}

