plugins {
  id 'com.github.johnrengelman.shadow' version '5.1.0'
}

dependencies {
  api project(':spectator-api')
  api project(':spectator-ext-gc')
  api project(':spectator-ext-jvm')
  implementation "com.eclipsesource.minimal-json:minimal-json"
  implementation "com.typesafe:config"
  implementation 'io.dropwizard.metrics:metrics-core:3.1.2'
  implementation 'org.apache.spark:spark-core_2.10:1.6.1'
}

jar {
  manifest {
    attributes(
      "Automatic-Module-Name": "com.netflix.spectator.spark"
    )
  }
}

shadowJar {
  classifier = 'shadow'

  relocate 'com.eclipsesource.json', 'com.netflix.spectator.shadowedjson'

  // The dependencies not listed here should come from the spark distribution
  dependencies {
    include project(':spectator-api')
    include project(':spectator-ext-gc')
    include project(':spectator-ext-jvm')
    include dependency("com.eclipsesource.minimal-json:minimal-json")
    include dependency("com.typesafe:config")
  }
}

publishing {
  publications {
    nebula(MavenPublication) {
      artifact shadowJar
    }
  }
}

